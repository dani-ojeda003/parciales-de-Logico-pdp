//https://docs.google.com/document/d/1JbGxnZa6CWHImsjE9SWcx6Ki9vBqQlKRfVX29DQAe9U/edit?tab=t.0#heading=h.xc9zmucugiv2 

% receta(Plato, Duraci칩n, Ingredientes)
receta(empanadaDeCarneFrita, 20, [harina, carne, cebolla, picante, aceite]).
receta(empanadaDeCarneAlHorno, 20, [harina, carne, cebolla, picante]).
receta(lomoALaWellington, 125, [lomo, hojaldre, huevo, mostaza]).
receta(pastaTrufada, 40, [spaghetti, crema, trufa]).
receta(souffleDeQueso, 35, [harina, manteca, leche, queso]).
receta(tiramisu, 30, [vainillas, cafe, mascarpone]).
receta(rabas, 20, [calamar, harina, sal]).
receta(parrilladaDelMar, 40, [salmon, langostinos, mejillones]).
receta(sushi, 30, [arroz, salmon, sesamo, algaNori]).
receta(hamburguesa, 15, [carne, pan, cheddar, huevo, panceta, trufa]).
receta(padThai, 40, [fideos, langostinos, vegetales]).

% elabora(Chef, Plato)
elabora(guille, empanadaDeCarneFrita).
elabora(guille, empanadaDeCarneAlHorno).
elabora(vale, rabas).
elabora(vale, tiramisu).
elabora(vale, parrilladaDelMar).
elabora(ale, hamburguesa).
elabora(lu, sushi).
elabora(mar, padThai).


% cocinaEn(Restaurante, Chef)
cocinaEn(pinpun, guille).
cocinaEn(laPececita, vale).
cocinaEn(laParolacha, vale).
cocinaEn(sushiRock, lu).
cocinaEn(olakease, lu).
cocinaEn(guendis, ale).
cocinaEn(cantin, mar).

% tieneEstilo(Restaurante, Estilo)
tieneEstilo(pinpun, bodegon(parqueChas, 6000)).
tieneEstilo(laPececita, bodegon(palermo, 20000)).
tieneEstilo(laParolacha, italiano(15)).
tieneEstilo(sushiRock, oriental(japon)).
tieneEstilo(olakease, oriental(japon)).
tieneEstilo(cantin, oriental(tailandia)).
tieneEstilo(cajaTaco, mexicano([habanero, rocoto])).
tieneEstilo(guendis, comidaRapida(5)).

% italiano(CantidadDePastas)
% oriental(Pa칤s)
% bodegon(Barrio, PrecioPromedio)
% mexicano(VariedadDeAjies)
% comidaRapida(cantidadDeCombos)

/*
1) 游땙esCrack/1: un o una chef es crack si trabaja en por lo menos
 dos restaurantes o cocina pad thai. */

esCrack(Chef):-
    cocinaEn(Restaurante,Chef),
    cocinaEn(OtroRestaurante,Chef),
    Restaurante \= OtroRestaurante.

esCrack(Chef):-
    elabora(Chef,padThai).

/*
2) 游꼧esOtaku/1: un o una chef es otaku cuando solo trabaja en 
restaurantes de comida japonesa. (Y le tiene que gustar Naruto, pero 
eso no lo vamos a modelar). */
esChef(Chef):-
    cocinaEn(_,Chef).

esOtaku(Chef):-
    esChef(Chef),
    forall(cocinaEn(Restaurante,Chef),tieneEstilo(Restaurante,oriental(japon))).


/*
3) 游댠esTop/1: un plato es top si s칩lo lo elaboran chefs cracks. */
esTop(Plato):-
    elabora(_,Plato),
    forall(elabora(Chef,Plato),esCrack(Chef)).
/*
4)游뱚esDificil/1: un plato es dif칤cil cuando tiene una duraci칩n de 
m치s de dos horas o tiene trufa como ingrediente o es un souffl칠 de 
queso. */
esDificil(Plato):-
    receta(Plato,Duracion,_),
    Duracion >120.
    
esDificil(Plato):-
    contieneIngrediente(trufa,Plato).

esDificil(souffleDeQueso).

contieneIngrediente(Ingrediente,Plato):-
    receta(Plato,_,Ingredientes),
    member(Ingrediente,Ingredientes).
/*
5)救납eMereceLaMichelin/1: un restaurante se merece la estrella 
Michelin cuando tiene un o una chef crack y su estilo de cocina es 
michelinero. Esto sucede cuando es un restaurante:

de comida oriental de Tailandia,
un bodeg칩n de Palermo,
italiano de m치s de 5 pastas,
mexicano que cocine, por lo menos, con aj칤 habanero y rocoto,
los de comida r치pida nunca ser치n michelineros.  */
seMereceLaMichelin(Restaurante):-
    tieneChefCrack(Restaurante),
    tieneEstiloMichelinero(Restaurante).

tieneChefCrack(Restaurante):-
    cocinaEn(Restaurante,Chef),
    esCrack(Chef).

tieneEstiloMichelinero(Restaurante):-
    tieneEstilo(Restaurante,Estilo),
    esEstiloMichelinero(Estilo).

esEstiloMichelinero(oriental(tailandia)).

esEstiloMichelinero(bodegon(palermo,_)).

esEstiloMichelinero(italiano(CantidadDePastas)):-
    CantidadDePastas > 5.


esEstiloMichelinero(mexicano(VariedadDeAjies)):-
    member(habanero, VariedadDeAjies),
    member(rocoto, VariedadDeAjies).
/*
游뉦잺tieneMayorRepertorio/2: seg칰n dos restaurantes, se cumple cuando 
el primero tiene un o una chef que elabora m치s platos que el o la 
chef del segundo. */
tieneMayorRepertorio(Restaurante,OtroRestaurante):-
    cocinaEn(Restaurante,Chef),
    cocinaEn(OtroRestaurante,OtroChef),
    Restaurante \= OtroRestaurante,
    tieneMayorCantidadDePlatos(Chef,OtroChef).

tieneMayorCantidadDePlatos(Chef,OtroChef):-
    Chef \= OtroChef,
    cantidadDePlatos(Chef,Cantidad),
    cantidadDePlatos(OtroChef,OtraCantidad),
    Cantidad > OtraCantidad.

cantidadDePlatos(Chef, CantidadDePlatos):-
    esChef(Chef),
    findall(Plato, elabora(Chef,Plato) ,ListaDePlatos),
    length(ListaDePlatos, CantidadDePlatos).
/*
7) 游녨calificacionGastronomica/2: la calificaci칩n de un restaurante 
es 5 veces la cantidad de platos que elabora el o la chef de este 
restaurante.  */
calificacionGastronomica(Restaurante,Calificacion):-
    cocinaEn(Restaurante,Chef),
    cantidadDePlatos(Chef,Cantidad),
    Calificacion is Cantidad *5.
    cantidadDePlatos(Chef,Cantidad),
    Calificacion is Cantidad *5.
